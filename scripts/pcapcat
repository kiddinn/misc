#!/usr/bin/perl
#################################################################################################
#             pcapcat 
#################################################################################################
# This script reads a PCAP file and prints out all the connections in the file and gives
# the user the option of dumping the content of the TCP stream
#
# Author: Kristinn Gudjonsson
# Version : 0.21
# Date : 30/09/09
#
# Copyright 2009 Kristinn Gudjonsson, kristinn ( a t ) log2timeline ( d o t ) net
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
#     Unless required by applicable law or agreed to in writing, software
#     distributed under the License is distributed on an "AS IS" BASIS,
#     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#     See the License for the specific language governing permissions and
#     limitations under the License.
#


use strict;
use Getopt::Long; # read parameters
use Pod::Usage;
use Net::Pcap;
use NetPacket::Ethernet;
use NetPacket::IP;
use NetPacket::TCP;
use NetPacket::UDP;

use vars qw($VERSION);

# version information
$VERSION = '0.21';

# define how many numbers of connections to show on each screen (default value)
my $buffer = 10;

# other variables (no not edit unless you know what you are doing)
my ($out,$file);
my $filter;
my $filter_c;
my $print_help;
my ($ether,$ip,$trans);
my $err;
my $packets;
my $index;
my $show_version;
my $dump_nr;
my $only_syn = 1;
my $all;
my $i = 0;
my $in_read;
my $use_buffer = 1;	# defines whether or not a buffer is used
my $top_index;		# defines the first index value printed on screen

# read options
GetOptions(
        "filter:s"=>\$filter,
	"buffer:s"=>\$buffer,
        "read:s"=>\$file,
	"write:s"=>\$out,
	"only-syn!"=>\$only_syn,
	"all!"=>\$all,
        "version!"=>\$show_version,
        "dump:s"=>\$dump_nr,
        "help|?!"=>\$print_help
) or pod2usage( 2 );

# check if we are asking for help
pod2usage(1) if $print_help;

# print versioning information
show_version() if $show_version;

# check if we are showing all packets
$only_syn = 0 if ( $all );

# check if file exists
pod2usage(2) unless -e $file;

# reset the index value
$index = 1;

# check the buffer variable
$buffer = int( $buffer );

# check if buffer eq to 0
$use_buffer = 0 if $buffer eq 0;
$buffer = 10 if $buffer < 1;

# start reading the file
$packets = Net::Pcap::open_offline( $file, \$err );
if( defined $err )
{
	die 'Unable to read the PCAP file ' . $err . "\n";
}


# now we need to check if we are going to read all packets or dump a certain traffic
if( defined $dump_nr )
{
	dump_index($dump_nr,'');
}
else
{
	# normal run, print all the packets in an "interactive" environment

	# check if there is a filter defined
	$filter = 'tcp' unless defined $filter;

	# create the filter
	Net::Pcap::compile( $packets, \$filter_c, $filter, 1, undef );
	Net::Pcap::setfilter( $packets, $filter_c );

	# read all the packets
	$i = 0;	# null the $i value
	Net::Pcap::loop( $packets, -1, \&read_all_packets, '' ); 

	# now we reached the end of the packets
	# check if there is a buffer size defined
	if( $use_buffer && $top_index ne $index )
	{
		print "Enter the index number of the conversation to dump or press enter to quit: ";
		$in_read = <STDIN>;
		chomp( $in_read );

		if( $in_read ne '' )
		{
			# verify index
			$in_read = int( $in_read );
	
			# check if it is available
			if( ($in_read > 0) && ( $in_read <= $i) )
			{
				print "Dumping index value $in_read\n";
	
				# need to close and re-open the file
				Net::Pcap::close( $packets );
				$packets = Net::Pcap::open_offline( $file, \$err );
				dump_index( $in_read, $filter );
			}
			else
			{
				print "Index [$in_read] not within range\n";
			}
		}
	}
	
	# close the network file (since we have finished our processing)
	Net::Pcap::close( $packets );
}

#########################################################################################################
#		routines
#########################################################################################################

sub dump_index($$)
{
	my $index_number = shift;
	my $f = shift;

	# dump a certain traffic

	# first check if out file is defined
	if( ! defined $out )
	{
		print 'Unable to determine output file' . "\n";
		print "Give the name of the output file: ";
		$out = <STDIN>;
		chomp( $out );
	}

	# try to open the out file (create a new one)
	print "Overwriting file: $out\n" if -e $out;
	open( OF, '>' . $out );

	binmode( OF );

	# now to dump a certain stream
	dump_stream( $index_number, $f );

	close( OF );
} 


# 	dump_stream
# this function takes as a parameter an index into the pcap file and reads all packets that belong
# to that particular stream
sub dump_stream
{
	my $in = shift;
	my $f = shift;
	my %header;
	my $packet;

	if( $f ne 'tcp' && $f ne '' )
	{
		$filter = $f;
	}
	elsif( $only_syn )
	{
		$filter = 'tcp[13] & 0x3f = 0x02';
	}
	else
	{
		# the filter we are looking for is just defined as a TCP
		$filter = 'tcp';

	}
		
	Net::Pcap::compile( $packets, \$filter_c, $filter, 1, undef );
	Net::Pcap::setfilter( $packets, $filter_c );
		
	# find the correct packet
	for( my $i=0; $i < $in; $i++ )
	{
		$packet = Net::Pcap::next( $packets, \%header );
	}

	# strip header information and get the data part
	$ether = NetPacket::Ethernet->decode( $packet );	
	$ip = NetPacket::IP->decode( $ether->{'data'} );
	$trans = NetPacket::TCP->decode( $ip->{'data'} );

	# now I need to read all the data part of the entire conversation 
	# and dump it into a file
	# construct a filter
	$filter = 'tcp and (host ' . $ip->{'src_ip'} . ' and host ' . $ip->{'dest_ip'} . ') and ( port ' . $trans->{'dest_port'} . ' and port ' . $trans->{'src_port'} . ')';
	Net::Pcap::compile( $packets, \$filter_c, $filter,1,undef);
	Net::Pcap::setfilter( $packets, $filter_c );

	# read all the packets that belong to this particular stream
	Net::Pcap::loop( $packets, -1, \&dump_to_file, '' );
	
	return 1;
}

#	dump_to_file
#
# A small function which reads packets created in the dump_stream function
# and prints them to a file, an output file, to contain the information
# found inside streams
sub dump_to_file
{
	my $user_data = shift;
	my $header = shift;
	my $pack = shift;

	# strip headers	
	$ether = NetPacket::Ethernet->decode( $pack );	
	$ip = NetPacket::IP->decode( $ether->{'data'} );
	$trans = NetPacket::TCP->decode( $ip->{'data'} );

	# and now to dump the content of the data variable into a file
	print OF $trans->{'data'};

	return 1;
}

#	read_all_packets
#
#
sub read_all_packets
{
	my $user_data = shift;
	my $header = shift;
	my $pack = shift;
	my $fcheck;
	my $input;

	# define the first value
	$top_index = $index if $i eq 0;

	# check if we have printed $buffer
	if( $use_buffer && $i == $buffer )
	{
		# print out a statement
		printf "Enter number between %d and %d to dump conversation, enter to continue or 0 to quit: ",$index-$buffer,$index-1;
		$input = <STDIN>;
		chomp( $input );

		if( lc( $input ) eq '0' )
		{
			# we quit
			print "Not printing out more packets\n";
			exit 0;	
		}	
		elsif( $input eq '' )
		{
			# we continue
			$i = 0;
			$top_index = $index;
			# clear the screen
			system $^O eq 'MSWin32' ? 'cls' : 'clear';

		}	
		else
		{
			# check the index value
			if( ( $input >= $index-$buffer ) && ( $input <= $index-1 ) )
			{
				dump_index( $input, '' );
			}
			exit 0;
		}
	}

	# strip header information
	$ether = NetPacket::Ethernet->decode( $pack );	
	
	# check if IP
#	if( $ether->{type} eq 2048 )
#	{
		$ip = NetPacket::IP->decode( $ether->{'data'} );

		# check if TCP or UDP
		if( $ip->{'proto'} eq 6 )
		{
			# TCP
			$trans = NetPacket::TCP->decode( $ip->{'data'} );

			# check if we are to dump "all" traffic or just show new connections
			if( $only_syn )
			{
				# we don't care about ECN bits
				$fcheck = $trans->{'flags'} & 0x3f;

				# check if we have a SYN packet
				if( $fcheck == 0x02  )
				{
					print '[',$index,'] ';
					
					print 'TCP ' , $ip->{'src_ip'}  , ':' , $trans->{'src_port'} ,  ' -> ' , $ip->{'dest_ip'} , ':' , $trans->{'dest_port'},"\n";
					# increment both the index variable as well as the (for printing)
					$index++;
					$i++;
				}
			}
			else
			{	
				# we show all connections
				print '[',$index,'] '; 
				print 'TCP ', $ip->{'src_ip'}  . ':' . $trans->{'src_port'} .  ' -> ' . $ip->{'dest_ip'} . ':' . $trans->{'dest_port'},'[',$trans->{'flags'},"]\n";
				# increment both the index variable as well as the (for printing)
				$index++;
				$i++;
			}

	
		}
#		In this version we do not care about packets that are not TCP
#		elsif( $ip->{'proto'} eq 17 )
#		{
#			# UDP
#			$trans = NetPacket::UDP->decode( $ip->{'data'} );
#			print 'UDP ' . $ip->{'src_ip'}  . ':' . $trans->{'src_port'} .  ' -> ' . $ip->{'dest_ip'} . ':' . $trans->{'dest_port'} . "\n";
#		}
#		else
#		{
#			print 'Not TCP nor UDP, perhaps ICMP? Protocol number is: ' . $ip->{'proto'} . "\n";
#		}

#	}
#	else
#	{
#		print 'Packet is not an IP packet, rather a ' . $ether->{type} . "\n";
#	}
}

# a simple sub routine to show 
sub show_version
{
	print $0,' version ',$VERSION, ' copyright 2009, Kristinn Gudjonsson',"\n";
	exit 0;
}

0;

__END__

=pod

=head1 NAME

B<pcapcat> - a simple script to read PCAP file and dump the content of the conversation into a file 

=head1 SYNOPSIS 

B<pcapcat> [-f|--filter PCAP FILTER] -r|--read PCAP_FILE [-a|-all]

B<pcapcat> -w|--write OUTPUTFILE [-a|--all] -d|-dump INDEX

B<pcapcat>[ -v|--version] [-h|--help|-?] 

=head1 OPTIONS

=over 8

=item B<-f|-filter PCAP_FILTER>

Enter a traditional PCAP filter to filter out the content of the file (see man tcpdump for further details about constructing such a filter)

=item B<-r|-read PCAP_FILE>

The PCAP file that the script should read

=item B<-a|-all>

The default behaviour of the script is to only show TCP SYN packets, that is to show entire conversations.  To override this option and provide the possibility to
dump an already started conversation use this option.

=item B<-w|-write FILE>

Use this option to define an output file to dump the content of the TCP stream into.  If the option -d or -dump is used, then this option has to be defined.

=item B<-d|-dump INDEX>

The default behaviour (if the -d option is not used) is to dump information about TCP connections found inside the pcap file.  In the printout an index number is written.
To be able to dump the content of a stream into a file you must first find out the index number and then use the -d INDEX option with that index number to dump that conversation
into a file.

=item B<-b|-buffer>

Increase the buffer size or the display of TCP connections (default 10).  If the buffer size is equal to 0 then we print out all the packets in one run, that is there is no buffer.

=item B<-v|-version>

Dump the version number of the script to the screen and quit

=item B<-h|-help|-?>

Print this help menu

=back
